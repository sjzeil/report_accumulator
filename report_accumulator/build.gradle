/*
 * Report Accumulator build file
 * Steven J Zeil, Old Dominion Univ.
 */

apply plugin: 'groovy'
apply plugin: 'java'
apply plugin: 'ivy-publish'
apply plugin: 'project-report'
apply plugin: 'checkstyle'
apply plugin: 'findbugs'
apply plugin: 'pmd'
apply plugin: 'jacoco'


buildscript {
	repositories {
		jcenter()
	}
	dependencies {
		classpath 'org.hidetake:gradle-ssh-plugin:2.2.0+'
	}
}

// In this section you declare where to find the dependencies of your project
repositories {
    // Use 'jcenter' for resolving your dependencies.
    // You can declare any Maven/Ivy/file repository here.
    jcenter()
}

// In this section you declare the dependencies for your production and test code
dependencies {
    compile gradleApi()
    compile localGroovy()
    compile 'org.hidetake:gradle-ssh-plugin:2.2.0+'
    testCompile 'junit:junit:4.12'
}

/*
publishing {
    publications {
        ivyJava(IvyPublication) {
            organisation 'edu.odu.cs.zeil'
            module project.name
            revision '1.0-SNAPSHOT'
            descriptor.status = 'milestone'
            descriptor.branch = 'master'

            from components.java
        }
    }
    repositories {
        ivy {
            name 'ivyRepo'
            url 'sftp://linux.cs.odu.edu:22/home/zeil/secure_html/ivyrepo'
            // Readable via https://www.cs.odu.edu/~zeil/ivyrepo    
            credentials {
                // Credentials are loaded from ~/.gradle/gradle.properties
                username "$ivyRepoUser"
                password "$ivyRepoPass"
            }
        }
    }           
}
*/

checkstyle {
    ignoreFailures = true
    showViolations = false    
}

findbugsMain {
    ignoreFailures = true
    effort = 'max'
    reportLevel = 'high'
    reports {
        xml.enabled = false
        html.enabled = true
    }
}

tasks.withType(Checkstyle) {
  reports {
    html.destination project.file("build/reports/checkstyle/main.html")
  }
}

pmd {
    ignoreFailures = true
    consoleOutput = false
}

test.ignoreFailures=true
checkstyleTest.enabled = false
findbugsTest.enabled = false
pmdTest.enabled = false
check.dependsOn jacocoTestReport
check.dependsOn htmlDependencyReport

task copyWebPages (type: Copy) {
    from 'src/main/html'
    into 'build/reports'
}

task copyDocs (type: Copy) {
    from 'build/docs'
    into 'build/reports/docs'
    dependsOn 'javadoc'
}

task reports (dependsOn: ['htmlDependencyReport', 'javadoc', 'check', 'copyWebPages', 'copyDocs']) {
    description 'Generate all reports for this project'
}

task reportStats (type: JavaExec) {
    dependsOn 'reports'
    classpath = sourceSets.test.runtimeClasspath
    main = 'edu.odu.cs.zeil.report_accumulator.Main';
    args = ['http://www.cs.odu.edu/~zeil/gitlab/reportAccumulator/reports/', 'build/reports']
}


task deployReports (type: Copy) {
    description 'copy all reports to project web page'
    dependsOn 'reportStats'
    from 'build/reports'
    into '/home/zeil/public_html/gitlab/' + project.name + '/reports'
}
